// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © RobotNeverDie

//@version=4
strategy("PSAR Strategy", shorttitle="PSAR Strategy v0.1.0", overlay=true, pyramiding=100, initial_capital=10000, default_qty_type=strategy.percent_of_equity, default_qty_value=3)

// Description:
// An trend strategy using Parabolic SAR.
// * Trend Following - A Moving Average.
// * Trend Confirmation - Parabolic SAR
// * Entry/Exit - None
// * Stop-Loss and Profit-Taking - ATR
// * Position Sizing - None
//
// More at https://github.com/robotneverdie-algo/Pine-Script

//{=================================================================================== Misc. Functions =====================================================================================}

source(x) =>
    if x == "open"
        open
    else
        if x == "high"
            high
        else
            if x == "low"
                low
            else
                if x == "close"
                    close
                else
                    if x == "hl2"
                        hl2
                    else
                        if x == "hlc3"
                            hlc3
                        else
                            if x == "ohlc4"
                                ohlc4
                            else
                                vwap

// String format of min tick value for the current symbol to eight decimal places.
min_tick_format() =>
    format = "#.#"
    for i = 1 to 8
        if pow(10, i) * syminfo.mintick < 1
            format := format + "#"
    format

// Draw an panel to display infomation on chart.
info_panel(x, y, panel_color, text_color, line_num, message) =>
    content = ""
    for l = line_num to 0
        content := content + "\n"
    content := content + message
    var label panel = na
    label.delete(panel)
    panel := label.new(x, y, text=content, xloc=xloc.bar_time, yloc=yloc.price, color=panel_color, textcolor=text_color, style=label.style_labelup)

// An selection function to return the bands [upper, lower] of selected type.
bands(x, type, len, mult) =>
    if type == "bollinger"
        // Bollinger Bands
        basis = sma(x, len)
        volatility = stdev(x, len)
        [basis + (volatility * mult), basis - (volatility * mult)]
    else
        if type == "keltner"
            // Keltner Channels
            basis = ema(x, len)
            volatility = atr(len)
            [basis + (volatility * mult), basis - (volatility * mult)]
        else
            if type == "atr"
                // Average True Range
                basis = x
                volatility = atr(len)
                [basis + (volatility * mult), basis - (volatility * mult)]
            else
                if type == "donchian"
                    // Donchian Channel
                    upper = highest(len)
                    lower = lowest(len)
                    basis = avg(upper, lower)
                    volatility = upper - lower
                    [basis + (volatility * mult), basis - (volatility * mult)]
                else
                    // Chandelier Exit
                    upper = highest(len)
                    lower = lowest(len)
                    volatility = atr(len)
                    [upper - (volatility * mult) , lower + (volatility * mult)]

// percentage difference
pct_diff(x, y) =>
    ((x - y) / avg(x, y)) * 100

// Position Size Formula
position_size(capital, risk, entry, stop) =>
    stop_distance = abs(pct_diff(entry, stop))
    risk_amount = capital * risk
    risk_amount / stop_distance

//{=============================================================================== Moving Average Functions ================================================================================}

// Coefficient of Variation Weighted Moving Average https://www.tradingview.com/script/0dBtJvhu-Stratus-DW/
cvwma(x, len) =>
    cv = stdev(x, len) / sma(x, len)
    w = x * cv
    sum(w, len) / sum(cv, len)

// Double Exponential Moving Average
dema(x, len) =>
    e = ema(x, len)
    2 * e - ema(e, len)

// Donchian Middle Channel
donchian(len) =>
    avg(lowest(len), highest(len))

// Error Corrected EMA http://www.mesasoftware.com/papers/ZeroLag.pdf
ecema(x, len, limit) =>
    a = 2 / (len + 1)
    e = 0.0
    e := a * x + (1 - a) * nz(e[1])
    ec = 0.0
    least_error = 1000000.0
    best_gain = 0.0
    for i = -limit to limit
        gain = i / 10
        ec := a * (e + gain * (x - ec[1])) + (1 - a) * nz(ec[1])
        error = x - ec
        if abs(error) < least_error
            least_error := abs(error)
            best_gain := gain
    ec := a * (e + best_gain * (x - nz(ec[1]))) + (1 - a) * nz(ec[1])

// clamp a number in an range
clamp(x, xmin, xmax) =>
    max(min(x, xmax), xmin)

// Fractal Adaptive Moving Average http://www.mesasoftware.com/papers/FRAMA.pdf
frama(x, len) =>
    half = int(len / 2)
    hh1 = for i = 1 to half
        max(high[i], high[i-1])
    ll1 = for i = 1 to half
        min(low[i], low[i-1])

    hh2 = for i = half + 1 to len
        max(high[i], high[i-1])
    ll2 = for i = half + 1 to len
        min(low[i], low[i-1])

    hh3 = for i = 1 to len
        max(high[i], high[i-1])
    ll3 = for i = 1 to len
        min(low[i], low[i-1])

    n1 = (hh1 - ll1) / half
    n2 = (hh2 - ll2) / half
    n3 = (hh3 - ll3) / len
    d = 0.0
    d := iff(n1 > 0 and n2 > 0 and n3 > 0, (log(n1 + n2) - log(n3)) / log(2), clamp(nz(d[1]), 1, 2))
    a = exp(-4.6 * (d - 1))
    alpha = clamp(nz(a), 0.01, 1)
    f = 0.0
    f := iff(bar_index > len + 1, alpha * x + (1 - alpha) * f[1], x)
    f

// Hull Moving Average
hull(x, len) =>
    wma(2 * wma(x, len / 2) - wma(x, len), round(sqrt(len)))

// Jurik Moving Average https://www.tradingview.com/script/nZuBWW9j-Jurik-Moving-Average/
jma(x, len, phase, power) =>
    // Copyright (c) 2007-present Jurik Research and Consulting. All rights reserved.
    // Copyright (c) 2018-present, Alex Orekhov (everget)
    ratio = phase < -100 ? 0.5 : phase > 100 ? 2.5 : phase / 100 + 1.5
    beta = 0.45 * (len - 1) / (0.45 * (len - 1) + 2)
    alpha = pow(beta, power)
    j = 0.0
    e0 = 0.0
    e1 = 0.0
    e2 = 0.0
    e0 := (1 - alpha) * x + alpha * nz(e0[1])
    e1 := (x - e0) * (1 - beta) + beta * nz(e1[1])
    e2 := (e0 + ratio * e1 - nz(j[1])) * pow(1 - alpha, 2) + pow(alpha, 2) * nz(e2[1])
    j := e2 + nz(j[1])
    j

// Kaufman Adaptive Moving Average
kama(x, len, fast, slow) =>
    f = 2 / (fast + 1)
    s = 2 / (slow + 1)
    change = abs(change(x, len))
    volatility = sum(abs(change(x)), len)
    er = change / volatility
    sc = pow(er * (f - s) + s, 2)
    k = 0.0
    k := nz(k[1], sma(x, len)) + sc * (x - nz(k[1], sma(x, len)))
    k

// Least Square Moving Average
lsma(x, len, offset) =>
   linreg(x, len, offset)

// McGinley Dynamic
mcginley(x, len, factor) =>
    md = 0.0
    md := na(md[1]) ? x : md[1] + (x - md[1]) / (factor * len * pow(x / md[1], 4))
    md

// Modified Fractal Adaptive Moving Average http://etfhq.com/blog/2010/09/30/fractal-adaptive-moving-average-frama/#Mod
modframa(x, len, fast, slow) =>
    half = int(len / 2)
    hh1 = for i = 1 to half
        max(high[i], high[i-1])
    ll1 = for i = 1 to half
        min(low[i], low[i-1])

    hh2 = for i = half + 1 to len
        max(high[i], high[i-1])
    ll2 = for i = half + 1 to len
        min(low[i], low[i-1])

    hh3 = for i = 1 to len
        max(high[i], high[i-1])
    ll3 = for i = 1 to len
        min(low[i], low[i-1])

    n1 = (hh1 - ll1) / half
    n2 = (hh2 - ll2) / half
    n3 = (hh3 - ll3) / len
    d = 0.0
    d := iff(n1 > 0 and n2 > 0 and n3 > 0, (log(n1 + n2) - log(n3)) / log(2), clamp(nz(d[1]), 1, 2))
    w = 2 / (slow + 1)
    olda = exp(log(w) * (d - 1))
    n = (2 - olda) / olda
    nn = (slow - fast) * (n - 1) / (slow - 1) + fast
    a = 2 / (nn + 1)
    alpha = clamp(nz(a), w, 1)
    f = 0.0
    f := iff(bar_index > len + 1, alpha * x + (1 - alpha) * f[1], x)
    f

// Smoothed Moving Average
smma(x, len) =>
    s = sma(x, len)
    s := (sum(x, len) - nz(s[1]) + x) / len
    s

// SuperSmoother Filter http://www.mesasoftware.com/papers/PredictiveIndicatorsForEffectiveTrading%20Strategies.pdf
supersmoother(x, len) =>
    pi = 3.14159
    y = exp(-sqrt(2) * pi / len)

    c = -y * y
    b = 2.0 * y * cos(sqrt(2) * pi / len)
    a = 1 - b - c

    z = 0.0
    z := iff(bar_index == 0, 0.0, iff(bar_index == 1, b * nz(z[1]), iff(bar_index == 2,  b * nz(z[1]) + c * nz(z[2]), a * ((x + nz(x[1])) / 2) + b * nz(z[1]) + c * nz(z[2]))))
    z

// Triple Exponential Moving Average
tema(x, len) =>
    e1 = ema(x, len)
    e2 = ema(e1, len)
    e3 = ema(e2, len)
    (3 * e1) - (3 * e2) + e3

// Volatility Adjusted Moving Average https://www.tradingview.com/script/ZHGCH1jf-Volatility-Adjusted-Moving-Average-JD/
vama(x, len, lookback) =>
    // © Copyright 2019 to present, Joris Duyck (JD)
    mean = ema(x, len)
    diff = x - mean
    up = highest(diff, lookback)
    down = lowest(diff, lookback)
    mean + avg(up, down)

// Variable Index Dynamic Average https://www.tradingview.com/script/hdrf0fXV-Variable-Index-Dynamic-Average-VIDYA/
vidya(x, len, ema_len) =>
    // Copyright (c) 2018-present, Alex Orekhov (everget)
    diff = change(x, len)
    up = diff > 0 ? diff : 0.0
    down  = diff < 0 ? abs(diff) : 0.0
    cmo = abs((cum(up) - cum(down)) / (cum(up) + cum(down)))
    a = 2 / (ema_len + 1)
    v = 0.0
    v := x * a * cmo + nz(v[1]) * (1 - a * cmo)
    v

// Zero Lag Exponential Moving Average https://www.fmlabs.com/reference/default.htm?url=ZeroLagExpMA.htm
zlema(x, len) =>
    a = 2 / (len + 1)
    lag = (len - 1) / 2
    e = 0.0
    e := a * (2 * x - x[lag]) + (1 - a) * nz(e[1])
    e

//{============================================================================= Backtest Date Range Setting ===============================================================================}

_backtest_header = input(true, type=input.bool, title="{================== Backtest Date Range Setting =================}")
_use_date_range = input(true, type=input.bool, title="Use Date Range")
_start_day = input(1, minval=1, maxval=31, title="Start Date")
_start_month = input(1, minval=1, maxval=12, title="Start Month")
_start_year = input(1950, minval=2011, title="Start Year")
_end_day = input(1, minval=1, maxval=31, title="End Date")
_end_month = input(1, minval=1, maxval=12, title="End Month")
_end_year = input(2050, minval=2019, title="End Year")

_start = timestamp(_start_year, _start_month, _start_day, 0, 0)
_end = timestamp(_end_year, _end_month, _end_day, 0, 0)

// Bar window Function
bar_window() =>
    if _use_date_range
        time >= _start and time <= _end
    else
        true

//{=================================================================================== Strategy Setting =====================================================================================}

_strategy_header = input(true, type=input.bool, title="{====================== Strategy Settings =======================}")

_trades_count = input(1, minval=1, title="Trades Count")
_src = input("ohlc4", options=["open", "high", "low", "close", "hl2", "hlc3", "ohlc4", "vwap"], title="Source")
_use_tf = input(false, type=input.bool, title="Use Different Timeframe")
_tf = input("240", type=input.resolution, title="Timeframe")
_market_position = input("Both", options=["Both", "Long", "Short"], title="Market Position")

//{================================================================================ Moving Average Setting =================================================================================}

_ma_header = input(true, type=input.bool, title="{==================== Moving Average Setting ====================}")
_ma_type = input("jma", options=["alma", "cvwma", "dema", "donchian", "ecema", "ema", "frama", "hull", "jma", "kama", "lsma", "mcginley", "modframa", "rma", "sma", "smma", "supersmoother", "tema", "vama", "vidya", "vwma", "wma", "zlema"], title="Moving Average: Type")
_ma_len = input(30, title="Moving Average: Length")

_alma_offset = input(0.8, step=0.01, minval=0.0, maxval=1.0, title="alma: offset")
_alma_sigma = input(10, title="alma: sigma")
_ecema_limit = input(50, title="ecema: gain limit")
_jma_phase = input(50, title="jma: phase")
_jma_power = input(2, title="jma: power")
_kama_fast = input(2, title="kama: fast")
_kama_slow = input(30, title="kama: slow")
_lsma_offset = input(0, minval=0, title="lsma: offset")
_mcginley_factor = input(0.6, step=0.01, minval=0.0, maxval=1.0, title="mcginley: smoothing factor")
_modframa_fast = input(1, title="modframa: fast")
_modframa_slow = input(198, title="modframa: slow")
_vama_lookback = input(50, title="vama: fast")
_vidya_len = input(30, title="vidya: ema length")

moving_average1(src, type, len) =>
    if type == "modframa"
        modframa(src, len, _modframa_fast, _modframa_slow)
    else
        if type == "rma"
            rma(src, len)
        else
            if type == "sma"
                sma(src, len)
            else
                if type == "smma"
                    smma(src, len)
                else
                    if type == "supersmoother"
                        supersmoother(src, len)
                    else
                        if type == "tema"
                            tema(src, len)
                        else
                            if type == "vama"
                                vama(src, len, _vama_lookback)
                            else
                                if type == "vidya"
                                    vidya(src, len, _vidya_len)
                                else
                                    if type == "vwma"
                                        vwma(src, len)
                                    else
                                        if type == "wma"
                                            wma(src, len)
                                        else
                                            zlema(src, len)

moving_average(src, type, len) =>
    if type == "alma"
        alma(src, len, _alma_offset, _alma_sigma)
    else
        if type == "cvwma"
            cvwma(src, len)
        else
            if type == "dema"
                dema(src, len)
            else
                if type == "donchian"
                    donchian(len)
                else
                    if type == "ecema"
                        ecema(src, len, _ecema_limit)
                    else
                        if type == "ema"
                            ema(src, len)
                        else
                            if type == "frama"
                                frama(src, len)
                            else
                                if type == "hull"
                                    hull(src, len)
                                else
                                    if type == "jma"
                                        jma(src, len, _jma_phase, _jma_power)
                                    else
                                        if type == "kama"
                                            kama(src, len, _kama_fast, _kama_slow)
                                        else
                                            if type == "lsma"
                                                lsma(src, len, _lsma_offset)
                                            else
                                                if type == "mcginley"
                                                    mcginley(src, len, _mcginley_factor)
                                                else
                                                    moving_average1(src, type, len)

//{================================================================================= Parabolic SAR Setting =================================================================================}

_psar_header = input(true, type=input.bool, title="{==================== Parabolic SAR Setting =====================}")
_psar_start = input(0.02, step=0.001, type=input.float, title="Start")
_psar_increment = input(0.02, step=0.001, type=input.float, title="Increment")
_psar_maximum = input(0.2, step=0.01, type=input.float, title="Maximum")

//{================================================================================== Entry/Exit Setting ===================================================================================}

_entry_exit_header = input(true, type=input.bool, title="{====================== Entry/Exit Setting ======================}")

//{========================================================================= Stop-Loss and Profit-Taking Setting ===========================================================================}

_sl_tp_header = input(true, type=input.bool, title="{============== Stop-Loss and Profit-Taking Setting =============}")
_use_sl_tp = input(true, type=input.bool, title="Use Stop-Loss and Profit-Taking")
_bands_type = input("atr", options=["bollinger", "keltner", "atr", "donchian", "chandelier"], title="Bands Type")
_bands_len = input(11, title="Bands Length")

_stp_mult = input(1, minval=0.1, step=0.1, title="Stop-Loss Multiplier")
_tp1_mult = input(1, minval=0.1, step=0.1, title="Take-Profit TP1 Multiplier")
_tp2_mult = input(2, minval=0.1, step=0.1, title="Take-Profit TP2 Multiplier")
_tp3_mult = input(3, minval=0.1, step=0.1, title="Take-Profit TP3 Multiplier")

_tp1_percent = input(50, minval=1, step=1, title="TP1: Reduce Position Percent")
_tp2_percent = input(30, minval=1, step=1, title="TP2: Reduce Position Percent")
_tp3_trailing = input(false, type=input.bool, title="TP3: Use Trailing Stop-Loss")

//{=============================================================================== Position Sizing Setting =================================================================================}

_position_sizing_header = input(true, type=input.bool, title="{==================== Position Sizing Setting ===================}")
_capital = input(10000, minval=0.00000001, title="Capital")
_risk = input(10, minval=0.01, maxval=100, step=0.01, title="Risk Percent") / 100

//{==================================================================================== Strategy Logic =====================================================================================}

_source = _use_tf ? security(syminfo.tickerid, _tf, source(_src)) : source(_src)
// Note: Unable to call security function with an multi variable function
[_stp_upper, _stp_lower] = bands(_source, _bands_type, _bands_len, _stp_mult)
[_tp1_upper, _tp1_lower] = bands(_source, _bands_type, _bands_len, _tp1_mult)
[_tp2_upper, _tp2_lower] = bands(_source, _bands_type, _bands_len, _tp2_mult)
[_tp3_upper, _tp3_lower] = bands(_source, _bands_type, _bands_len, _tp3_mult)

_ma = moving_average(_source, _ma_type, _ma_len)
_psar = sar(_psar_start, _psar_increment, _psar_maximum)

//{=================================================================================== Strategy Signal =====================================================================================}

_long = _source > _ma and _psar < close
_short = _source < _ma and _psar > close

_xlong = false
_xshort = false

var _long_count = 0
var _short_count = 0

var _when_long = 0
var _when_short = 0
var _when_exit = 0

_in_long = _when_long > _when_short and _when_long > _when_exit
_in_short = _when_short > _when_long and _when_short > _when_exit
_in_exit = _when_exit > _when_long and _when_exit > _when_short

if _long
    if _in_short or _in_exit
        _long_count := 1
    else
        _long_count := nz(_long_count[1]) + 1
    _short_count := 0
    _in_short := false
    _in_exit := false
    if _long_count <= _trades_count
        _when_long := time_close

if _short
    if _in_long or _in_exit
        _short_count := 1
    else
        _short_count := nz(_short_count[1]) + 1
    _long_count := 0
    _in_long := false
    _in_exit := false
    if _short_count <= _trades_count
        _when_short := time_close

if (_xlong and _in_long) or (_xshort and _in_short)
    _when_exit := time_close

_enter_long = _when_long == time_close and bar_window() and (_market_position == "Long" or _market_position == "Both")
_enter_short = _when_short == time_close and bar_window() and (_market_position == "Short" or _market_position == "Both")
_exit_trade = _when_exit == time_close

var _entry_price = 0.0
var _position_size = 0.0
var float _stop = na
var float _tp1 = na
var float _tp2 = na
var float _tp3 = na

var float _hit_stop = na
var float _hit_tp1 = na
var float _hit_tp2 = na
var float _hit_tp3 = na

if _enter_long or _in_long
    if _enter_long
        _entry_price := close
        _stop := _stp_lower
        _position_size := position_size(_capital, _risk, _entry_price, _stop)
        _tp1 := _tp1_upper
        _tp2 := _tp2_upper
        _tp3 := _tp3_upper
        _hit_stop := na
        _hit_tp1 := na
        _hit_tp2 := na
        _hit_tp3 := na

    if _use_sl_tp and _in_long
        _hit_stop := crossunder(low, _stop) ? _stop : na
        _hit_tp1 := crossover(high, _tp1) ? _tp1 : na
        _hit_tp2 := crossover(high, _tp2) ? _tp2 : na
        _hit_tp3 := crossover(high, _tp3) ? _tp3 : na
else
    if _enter_short or _in_short
        if _enter_short
            _entry_price := close
            _stop := _stp_upper
            _position_size := position_size(_capital, _risk, _entry_price, _stop)
            _tp1 := _tp1_lower
            _tp2 := _tp2_lower
            _tp3 := _tp3_lower
            _hit_stop := na
            _hit_tp1 := na
            _hit_tp2 := na
            _hit_tp3 := na

        if _use_sl_tp and _in_short
            _hit_stop := crossover(high, _stop) ? _stop : na
            _hit_tp1 := crossunder(low, _tp1) ? _tp1 : na
            _hit_tp2 := crossunder(low, _tp2) ? _tp2 : na
            _hit_tp3 := crossunder(low, _tp3) ? _tp3 : na
    else
        if _exit_trade or _in_exit
            _entry_price := na
            _stop := na
            _tp1 := na
            _tp2 := na
            _tp3 := na
            _hit_stop := na
            _hit_tp1 := na
            _hit_tp2 := na
            _hit_tp3 := na

if _use_sl_tp and (_in_long or _in_short)
    if not na(_hit_stop)
        _when_exit := time_close
    else
        // when tp1 is hit move stop to entry price.
        if not na(_hit_tp1)
            _stop := _entry_price
            _tp1 := na
        else
            // when tp2 is hit move stop to tp1 price.
            if not na(_hit_tp2)
                _stop := valuewhen(na(_tp1), _hit_tp1[1], 0)
                _tp2 := na
            else
                // when tp3 is hit and trailing is on use an trailing stop.
                if _tp3_trailing and not na(_hit_tp3)
                    _stop := _in_long ? _stp_lower : _stp_upper
                    _tp3 := na
                else
                    // if we havn't exit when tp3 was hit update the trailing stop.
                    _in_tp3 = _in_long ? valuewhen(not na(_hit_tp3), time, 0) > _when_long : valuewhen(not na(_hit_tp3), time, 0) > _when_short
                    if _tp3_trailing and _in_tp3
                        _stop := _in_long ? _stp_lower : _stp_upper

strategy.entry("Long", strategy.long, when=_enter_long)
strategy.entry("Short", strategy.short, when=_enter_short)
strategy.exit("TP1", "Long", limit=valuewhen(not na(_tp1), _tp1, 0), qty_percent=_tp1_percent, when=_in_long and _use_sl_tp)
strategy.exit("TP2", "Long", limit=valuewhen(not na(_tp2), _tp2, 0), qty_percent=_tp2_percent, when=_in_long and _use_sl_tp)
strategy.exit("TP1", "Short", limit=valuewhen(not na(_tp1), _tp1, 0), qty_percent=_tp1_percent, when=_in_short and _use_sl_tp)
strategy.exit("TP2", "Short", limit=valuewhen(not na(_tp2), _tp2, 0), qty_percent=_tp2_percent, when=_in_short and _use_sl_tp)
strategy.close_all(when=_exit_trade or not na(_hit_stop) or (not _tp3_trailing and not na(_hit_tp3)))

//{==================================================================================== Strategy Draw ======================================================================================}

plot(_psar, style=plot.style_circles, linewidth=2, color=_psar < close ? #686ae5 : #feff54, transp=0, title="PSAR")
plot(_ma, linewidth=2, color=_source > _ma ? #1ca168 : #ce2020, transp=0, title="MA")

plotshape(_enter_long, style=shape.triangleup, location=location.bottom, color=#1ca168)
plotshape(_enter_short, style=shape.triangleup, location=location.bottom, color=#ce2020)
plotshape(_exit_trade, style=shape.triangledown, location=location.bottom, color=#ffc100)
// barcolor(_enter_long ? #1ca168 : _enter_short ? #ce2020 : _exit_trade ? #ffc100 : na)

plotchar(_use_sl_tp ? _hit_stop : na, textcolor=#ffff33, location=location.absolute, char='', text="Stop")
plotchar(_use_sl_tp ? _hit_tp1 : na, textcolor=#ffff33, location=location.absolute, char='', text="TP1")
plotchar(_use_sl_tp ? _hit_tp2 : na, textcolor=#ffff33, location=location.absolute, char='', text="TP2")
plotchar(_use_sl_tp ? _hit_tp3 : na, textcolor=#ffff33, location=location.absolute, char='', text="TP3")

plot(_use_sl_tp ? _stop : na, style=plot.style_cross, linewidth=2, color=#ce2020, transp=0, title="Stop")
plot(_use_sl_tp ? _tp1 : na, style=plot.style_cross, linewidth=2, color=#1ca168, transp=0, title="Tp1")
plot(_use_sl_tp ? _tp2 : na, style=plot.style_cross, linewidth=2, color=#1ca168, transp=0, title="Tp2")
plot(_use_sl_tp ? _tp3 : na, style=plot.style_circles, linewidth=2, color=#1ca168, transp=0, title="Tp3")

// Draw an info panel for Stop-Loss, Profit-Taking and Position Sizing
_panel_x = time + round(change(time) * 20)
_panel_y = highest(50)
_panel_color = _in_long ? #1ca168 : _in_short ? #ce2020 : #051c4d
_text_color = #ffc100
_price_format = min_tick_format()

info_panel(_panel_x, _panel_y, _panel_color, _text_color, 20, "{=========== {\\/} ===========}")
info_panel(_panel_x, _panel_y, _panel_color, _text_color, 18, "TP3: " + tostring(_tp3, _price_format))
info_panel(_panel_x, _panel_y, _panel_color, _text_color, 16, "TP2: " + tostring(_tp2, _price_format))
info_panel(_panel_x, _panel_y, _panel_color, _text_color, 14, "TP1: " + tostring(_tp1, _price_format))
info_panel(_panel_x, _panel_y, _panel_color, _text_color, 12, "Stop: " + tostring(_stop, _price_format))
info_panel(_panel_x, _panel_y, _panel_color, _text_color, 10, "Entry: " + tostring(_entry_price, _price_format))
info_panel(_panel_x, _panel_y, _panel_color, _text_color, 8, "Stop Distance: " + tostring(abs(pct_diff(_entry_price, _stop)) / 100, "#.##%"))
info_panel(_panel_x, _panel_y, _panel_color, _text_color, 6, "Position Size: " + tostring(_position_size, _price_format))
info_panel(_panel_x, _panel_y, _panel_color, _text_color, 4, "Risk: " + tostring(_risk, "#.##%"))
info_panel(_panel_x, _panel_y, _panel_color, _text_color, 2, "Capital: " + tostring(_capital, _price_format))
info_panel(_panel_x, _panel_y, _panel_color, _text_color, 0, "{=========== {\\/} ===========}")
