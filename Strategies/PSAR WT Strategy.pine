// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © RobotNeverDie

//@version=4
strategy("PSAR WT Strategy", shorttitle="PSAR WT Strategy v0.1.0", overlay=true, pyramiding=100, initial_capital=10000, default_qty_type=strategy.percent_of_equity, default_qty_value=3)

// Description:
// An trend strategy using Parabolic SAR and WaveTrend Oscillator.
// * Trend Following - Parabolic SAR.
// * Trend Confirmation - WaveTrend Oscillator.
// * Entry/Exit - IFish TCI Indicator
// * Stop-Loss and Profit-Taking - None
// * Position Sizing - None
//
// More at https://github.com/robotneverdie-algo/Pine-Script

//{=================================================================================== Misc. Functions =====================================================================================}

source(x) =>
    if x == "open"
        open
    else
        if x == "high"
            high
        else
            if x == "low"
                low
            else
                if x == "close"
                    close
                else
                    if x == "hl2"
                        hl2
                    else
                        if x == "hlc3"
                            hlc3
                        else
                            if x == "ohlc4"
                                ohlc4
                            else
                                vwap

//{=============================================================================== Moving Average Functions ================================================================================}

// Coefficient of Variation Weighted Moving Average https://www.tradingview.com/script/0dBtJvhu-Stratus-DW/
cvwma(x, len) =>
    cv = stdev(x, len) / sma(x, len)
    w = x * cv
    sum(w, len) / sum(cv, len)

// Double Exponential Moving Average
dema(x, len) =>
    e = ema(x, len)
    2 * e - ema(e, len)

// Donchian Middle Channel
donchian(len) =>
    avg(lowest(len), highest(len))

// Error Corrected EMA http://www.mesasoftware.com/papers/ZeroLag.pdf
ecema(x, len, limit) =>
    a = 2 / (len + 1)
    e = 0.0
    e := a * x + (1 - a) * nz(e[1])
    ec = 0.0
    least_error = 1000000.0
    best_gain = 0.0
    for i = -limit to limit
        gain = i / 10
        ec := a * (e + gain * (x - ec[1])) + (1 - a) * nz(ec[1])
        error = x - ec
        if abs(error) < least_error
            least_error := abs(error)
            best_gain := gain
    ec := a * (e + best_gain * (x - nz(ec[1]))) + (1 - a) * nz(ec[1])

// clamp a number in an range
clamp(x, xmin, xmax) =>
    max(min(x, xmax), xmin)

// Fractal Adaptive Moving Average http://www.mesasoftware.com/papers/FRAMA.pdf
frama(x, len) =>
    half = int(len / 2)
    hh1 = for i = 1 to half
        max(high[i], high[i-1])
    ll1 = for i = 1 to half
        min(low[i], low[i-1])

    hh2 = for i = half + 1 to len
        max(high[i], high[i-1])
    ll2 = for i = half + 1 to len
        min(low[i], low[i-1])

    hh3 = for i = 1 to len
        max(high[i], high[i-1])
    ll3 = for i = 1 to len
        min(low[i], low[i-1])

    n1 = (hh1 - ll1) / half
    n2 = (hh2 - ll2) / half
    n3 = (hh3 - ll3) / len
    d = 0.0
    d := iff(n1 > 0 and n2 > 0 and n3 > 0, (log(n1 + n2) - log(n3)) / log(2), clamp(nz(d[1]), 1, 2))
    a = exp(-4.6 * (d - 1))
    alpha = clamp(nz(a), 0.01, 1)
    f = 0.0
    f := iff(bar_index > len + 1, alpha * x + (1 - alpha) * f[1], x)
    f

// Hull Moving Average
hull(x, len) =>
    wma(2 * wma(x, len / 2) - wma(x, len), round(sqrt(len)))

// Jurik Moving Average https://www.tradingview.com/script/nZuBWW9j-Jurik-Moving-Average/
jma(x, len, phase, power) =>
    // Copyright (c) 2007-present Jurik Research and Consulting. All rights reserved.
    // Copyright (c) 2018-present, Alex Orekhov (everget)
    ratio = phase < -100 ? 0.5 : phase > 100 ? 2.5 : phase / 100 + 1.5
    beta = 0.45 * (len - 1) / (0.45 * (len - 1) + 2)
    alpha = pow(beta, power)
    j = 0.0
    e0 = 0.0
    e1 = 0.0
    e2 = 0.0
    e0 := (1 - alpha) * x + alpha * nz(e0[1])
    e1 := (x - e0) * (1 - beta) + beta * nz(e1[1])
    e2 := (e0 + ratio * e1 - nz(j[1])) * pow(1 - alpha, 2) + pow(alpha, 2) * nz(e2[1])
    j := e2 + nz(j[1])
    j

// Kaufman Adaptive Moving Average
kama(x, len, fast, slow) =>
    f = 2 / (fast + 1)
    s = 2 / (slow + 1)
    change = abs(change(x, len))
    volatility = sum(abs(change(x)), len)
    er = change / volatility
    sc = pow(er * (f - s) + s, 2)
    k = 0.0
    k := nz(k[1], sma(x, len)) + sc * (x - nz(k[1], sma(x, len)))
    k

// Least Square Moving Average
lsma(x, len, offset) =>
   linreg(x, len, offset)

// McGinley Dynamic
mcginley(x, len, factor) =>
    md = 0.0
    md := na(md[1]) ? x : md[1] + (x - md[1]) / (factor * len * pow(x / md[1], 4))
    md

// Modified Fractal Adaptive Moving Average http://etfhq.com/blog/2010/09/30/fractal-adaptive-moving-average-frama/#Mod
modframa(x, len, fast, slow) =>
    half = int(len / 2)
    hh1 = for i = 1 to half
        max(high[i], high[i-1])
    ll1 = for i = 1 to half
        min(low[i], low[i-1])

    hh2 = for i = half + 1 to len
        max(high[i], high[i-1])
    ll2 = for i = half + 1 to len
        min(low[i], low[i-1])

    hh3 = for i = 1 to len
        max(high[i], high[i-1])
    ll3 = for i = 1 to len
        min(low[i], low[i-1])

    n1 = (hh1 - ll1) / half
    n2 = (hh2 - ll2) / half
    n3 = (hh3 - ll3) / len
    d = 0.0
    d := iff(n1 > 0 and n2 > 0 and n3 > 0, (log(n1 + n2) - log(n3)) / log(2), clamp(nz(d[1]), 1, 2))
    w = 2 / (slow + 1)
    olda = exp(log(w) * (d - 1))
    n = (2 - olda) / olda
    nn = (slow - fast) * (n - 1) / (slow - 1) + fast
    a = 2 / (nn + 1)
    alpha = clamp(nz(a), w, 1)
    f = 0.0
    f := iff(bar_index > len + 1, alpha * x + (1 - alpha) * f[1], x)
    f

// Smoothed Moving Average
smma(x, len) =>
    s = sma(x, len)
    s := (sum(x, len) - nz(s[1]) + x) / len
    s

// SuperSmoother Filter http://www.mesasoftware.com/papers/PredictiveIndicatorsForEffectiveTrading%20Strategies.pdf
supersmoother(x, len) =>
    pi = 3.14159
    y = exp(-sqrt(2) * pi / len)

    c = -y * y
    b = 2.0 * y * cos(sqrt(2) * pi / len)
    a = 1 - b - c

    z = 0.0
    z := iff(bar_index == 0, 0.0, iff(bar_index == 1, b * nz(z[1]), iff(bar_index == 2,  b * nz(z[1]) + c * nz(z[2]), a * ((x + nz(x[1])) / 2) + b * nz(z[1]) + c * nz(z[2]))))
    z

// Triple Exponential Moving Average
tema(x, len) =>
    e1 = ema(x, len)
    e2 = ema(e1, len)
    e3 = ema(e2, len)
    (3 * e1) - (3 * e2) + e3

// Volatility Adjusted Moving Average https://www.tradingview.com/script/ZHGCH1jf-Volatility-Adjusted-Moving-Average-JD/
vama(x, len, lookback) =>
    // © Copyright 2019 to present, Joris Duyck (JD)
    mean = ema(x, len)
    diff = x - mean
    up = highest(diff, lookback)
    down = lowest(diff, lookback)
    mean + avg(up, down)

// Variable Index Dynamic Average https://www.tradingview.com/script/hdrf0fXV-Variable-Index-Dynamic-Average-VIDYA/
vidya(x, len, ema_len) =>
    // Copyright (c) 2018-present, Alex Orekhov (everget)
    diff = change(x, len)
    up = diff > 0 ? diff : 0.0
    down  = diff < 0 ? abs(diff) : 0.0
    cmo = abs((cum(up) - cum(down)) / (cum(up) + cum(down)))
    a = 2 / (ema_len + 1)
    v = 0.0
    v := x * a * cmo + nz(v[1]) * (1 - a * cmo)
    v

// Zero Lag Exponential Moving Average https://www.fmlabs.com/reference/default.htm?url=ZeroLagExpMA.htm
zlema(x, len) =>
    a = 2 / (len + 1)
    lag = (len - 1) / 2
    e = 0.0
    e := a * (2 * x - x[lag]) + (1 - a) * nz(e[1])
    e

//{============================================================================= Backtest Date Range Setting ===============================================================================}

_backtest_header = input(true, type=input.bool, title="{================== Backtest Date Range Setting =================}")
_use_date_range = input(true, type=input.bool, title="Use Date Range")
_start_day = input(1, minval=1, maxval=31, title="Start Date")
_start_month = input(1, minval=1, maxval=12, title="Start Month")
_start_year = input(1950, minval=2011, title="Start Year")
_end_day = input(1, minval=1, maxval=31, title="End Date")
_end_month = input(1, minval=1, maxval=12, title="End Month")
_end_year = input(2050, minval=2019, title="End Year")

_start = timestamp(_start_year, _start_month, _start_day, 0, 0)
_end = timestamp(_end_year, _end_month, _end_day, 0, 0)

// Bar window Function
bar_window() =>
    if _use_date_range
        time >= _start and time <= _end
    else
        true

//{=================================================================================== Strategy Setting =====================================================================================}

_strategy_header = input(true, type=input.bool, title="{====================== Strategy Settings =======================}")
_trades_count = input(1, minval=1, title="Trades Count")
_src = input("vwap", options=["open", "high", "low", "close", "hl2", "hlc3", "ohlc4", "vwap"], title="Source")
_use_tf = input(false, type=input.bool, title="Use Different Timeframe")
_tf = input("240", type=input.resolution, title="Timeframe")
_market_position = input("Both", options=["Both", "Long", "Short"], title="Market Position")

//{================================================================================= Parabolic SAR Setting =================================================================================}

_psar_header = input(true, type=input.bool, title="{==================== Parabolic SAR Setting =====================}")
_psar_start = input(0.02, step=0.001, type=input.float, title="Start")
_psar_increment = input(0.04, step=0.001, type=input.float, title="Increment")
_psar_maximum = input(0.2, step=0.01, type=input.float, title="Maximum")

//{=================================================================================== WaveTrend Setting ===================================================================================}

_wt_header = input(true, type=input.bool, title="{====================== WaveTrend Setting =======================}")

_channel_len = input(9, "Channel Length")

//{=============================================================================== WaveTrend Fast MA Setting ===============================================================================}

_fast_header = input(true, type=input.bool, title="{================== WaveTrend Fast MA Setting ===================}")

_fast_type = input("zlema", options=["alma", "cvwma", "dema", "donchian", "ecema", "ema", "frama", "hull", "jma", "kama", "lsma", "mcginley", "modframa", "rma", "sma", "smma", "supersmoother", "tema", "vama", "vidya", "vwma", "wma", "zlema"], title="Type")
_fast_len = input(14, title="Length")

_fast_alma_offset = input(0.8, step=0.01, minval=0.0, maxval=1.0, title="alma: offset")
_fast_alma_sigma = input(10, title="alma: sigma")
_fast_ecema_limit = input(50, title="ecema: gain limit")
_fast_jma_phase = input(50, title="jma: phase")
_fast_jma_power = input(2, title="jma: power")
_fast_kama_fast = input(2, title="kama: fast")
_fast_kama_slow = input(30, title="kama: slow")
_fast_lsma_offset = input(0, minval=0, title="lsma: offset")
_fast_mcginley_factor = input(0.6, step=0.01, minval=0.0, maxval=1.0, title="mcginley: smoothing factor")
_fast_modframa_fast = input(1, title="modframa: fast")
_fast_modframa_slow = input(198, title="modframa: slow")
_fast_vama_lookback = input(50, title="vama: fast")
_fast_vidya_len = input(30, title="vidya: ema length")

fast_moving_average1(src, type, len) =>
    if type == "modframa"
        modframa(src, len, _fast_modframa_fast, _fast_modframa_slow)
    else
        if type == "rma"
            rma(src, len)
        else
            if type == "sma"
                sma(src, len)
            else
                if type == "smma"
                    smma(src, len)
                else
                    if type == "supersmoother"
                        supersmoother(src, len)
                    else
                        if type == "tema"
                            tema(src, len)
                        else
                            if type == "vama"
                                vama(src, len, _fast_vama_lookback)
                            else
                                if type == "vidya"
                                    vidya(src, len, _fast_vidya_len)
                                else
                                    if type == "vwma"
                                        vwma(src, len)
                                    else
                                        if type == "wma"
                                            wma(src, len)
                                        else
                                            zlema(src, len)

fast_moving_average(src, type, len) =>
    if type == "alma"
        alma(src, len, _fast_alma_offset, _fast_alma_sigma)
    else
        if type == "cvwma"
            cvwma(src, len)
        else
            if type == "dema"
                dema(src, len)
            else
                if type == "donchian"
                    donchian(len)
                else
                    if type == "ecema"
                        ecema(src, len, _fast_ecema_limit)
                    else
                        if type == "ema"
                            ema(src, len)
                        else
                            if type == "frama"
                                frama(src, len)
                            else
                                if type == "hull"
                                    hull(src, len)
                                else
                                    if type == "jma"
                                        jma(src, len, _fast_jma_phase, _fast_jma_power)
                                    else
                                        if type == "kama"
                                            kama(src, len, _fast_kama_fast, _fast_kama_slow)
                                        else
                                            if type == "lsma"
                                                lsma(src, len, _fast_lsma_offset)
                                            else
                                                if type == "mcginley"
                                                    mcginley(src, len, _fast_mcginley_factor)
                                                else
                                                    fast_moving_average1(src, type, len)

//{=============================================================================== WaveTrend Slow MA Setting ===============================================================================}

_slow_header = input(true, type=input.bool, title="{================== WaveTrend Slow MA Setting ===================}")

_slow_type = input("hull", options=["alma", "cvwma", "dema", "donchian", "ecema", "ema", "frama", "hull", "jma", "kama", "lsma", "mcginley", "modframa", "rma", "sma", "smma", "supersmoother", "tema", "vama", "vidya", "vwma", "wma", "zlema"], title="Type")
_slow_len = input(4, title="Length")

_slow_alma_offset = input(0.8, step=0.01, minval=0.0, maxval=1.0, title="alma: offset")
_slow_alma_sigma = input(10, title="alma: sigma")
_slow_ecema_limit = input(50, title="ecema: gain limit")
_slow_jma_phase = input(50, title="jma: phase")
_slow_jma_power = input(2, title="jma: power")
_slow_kama_fast = input(2, title="kama: fast")
_slow_kama_slow = input(30, title="kama: slow")
_slow_lsma_offset = input(0, minval=0, title="lsma: offset")
_slow_mcginley_factor = input(0.6, step=0.01, minval=0.0, maxval=1.0, title="mcginley: smoothing factor")
_slow_modframa_fast = input(1, title="modframa: fast")
_slow_modframa_slow = input(198, title="modframa: slow")
_slow_vama_lookback = input(50, title="vama: fast")
_slow_vidya_len = input(30, title="vidya: ema length")

slow_moving_average1(src, type, len) =>
    if type == "modframa"
        modframa(src, len, _slow_modframa_fast, _slow_modframa_slow)
    else
        if type == "rma"
            rma(src, len)
        else
            if type == "sma"
                sma(src, len)
            else
                if type == "smma"
                    smma(src, len)
                else
                    if type == "supersmoother"
                        supersmoother(src, len)
                    else
                        if type == "tema"
                            tema(src, len)
                        else
                            if type == "vama"
                                vama(src, len, _slow_vama_lookback)
                            else
                                if type == "vidya"
                                    vidya(src, len, _slow_vidya_len)
                                else
                                    if type == "vwma"
                                        vwma(src, len)
                                    else
                                        if type == "wma"
                                            wma(src, len)
                                        else
                                            zlema(src, len)

slow_moving_average(src, type, len) =>
    if type == "alma"
        alma(src, len, _slow_alma_offset, _slow_alma_sigma)
    else
        if type == "cvwma"
            cvwma(src, len)
        else
            if type == "dema"
                dema(src, len)
            else
                if type == "donchian"
                    donchian(len)
                else
                    if type == "ecema"
                        ecema(src, len, _slow_ecema_limit)
                    else
                        if type == "ema"
                            ema(src, len)
                        else
                            if type == "frama"
                                frama(src, len)
                            else
                                if type == "hull"
                                    hull(src, len)
                                else
                                    if type == "jma"
                                        jma(src, len, _slow_jma_phase, _slow_jma_power)
                                    else
                                        if type == "kama"
                                            kama(src, len, _slow_kama_fast, _slow_kama_slow)
                                        else
                                            if type == "lsma"
                                                lsma(src, len, _slow_lsma_offset)
                                            else
                                                if type == "mcginley"
                                                    mcginley(src, len, _slow_mcginley_factor)
                                                else
                                                    slow_moving_average1(src, type, len)

//{=========================================================================== Trend Confirmation Index Setting ============================================================================}

_ma_header = input(true, type=input.bool, title="{=============== Trend Confirmation Index Setting ===============}")
_ma_type = input("jma", options=["alma", "cvwma", "dema", "donchian", "ecema", "ema", "frama", "hull", "jma", "kama", "lsma", "mcginley", "modframa", "rma", "sma", "smma", "supersmoother", "tema", "vama", "vidya", "vwma", "wma", "zlema"], title="Type")
_ma_len = input(70, title="Length")

_alma_offset = input(0.8, step=0.01, minval=0.0, maxval=1.0, title="alma: offset")
_alma_sigma = input(10, title="alma: sigma")
_ecema_limit = input(50, title="ecema: gain limit")
_jma_phase = input(50, title="jma: phase")
_jma_power = input(2, title="jma: power")
_kama_fast = input(2, title="kama: fast")
_kama_slow = input(30, title="kama: slow")
_lsma_offset = input(0, minval=0, title="lsma: offset")
_mcginley_factor = input(0.6, step=0.01, minval=0.0, maxval=1.0, title="mcginley: smoothing factor")
_modframa_fast = input(1, title="modframa: fast")
_modframa_slow = input(198, title="modframa: slow")
_vama_lookback = input(50, title="vama: fast")
_vidya_len = input(30, title="vidya: ema length")

moving_average1(src, type, len) =>
    if type == "modframa"
        modframa(src, len, _modframa_fast, _modframa_slow)
    else
        if type == "rma"
            rma(src, len)
        else
            if type == "sma"
                sma(src, len)
            else
                if type == "smma"
                    smma(src, len)
                else
                    if type == "supersmoother"
                        supersmoother(src, len)
                    else
                        if type == "tema"
                            tema(src, len)
                        else
                            if type == "vama"
                                vama(src, len, _vama_lookback)
                            else
                                if type == "vidya"
                                    vidya(src, len, _vidya_len)
                                else
                                    if type == "vwma"
                                        vwma(src, len)
                                    else
                                        if type == "wma"
                                            wma(src, len)
                                        else
                                            zlema(src, len)

moving_average(src, type, len) =>
    if type == "alma"
        alma(src, len, _alma_offset, _alma_sigma)
    else
        if type == "cvwma"
            cvwma(src, len)
        else
            if type == "dema"
                dema(src, len)
            else
                if type == "donchian"
                    donchian(len)
                else
                    if type == "ecema"
                        ecema(src, len, _ecema_limit)
                    else
                        if type == "ema"
                            ema(src, len)
                        else
                            if type == "frama"
                                frama(src, len)
                            else
                                if type == "hull"
                                    hull(src, len)
                                else
                                    if type == "jma"
                                        jma(src, len, _jma_phase, _jma_power)
                                    else
                                        if type == "kama"
                                            kama(src, len, _kama_fast, _kama_slow)
                                        else
                                            if type == "lsma"
                                                lsma(src, len, _lsma_offset)
                                            else
                                                if type == "mcginley"
                                                    mcginley(src, len, _mcginley_factor)
                                                else
                                                    moving_average1(src, type, len)

//{==================================================================================== Strategy Logic =====================================================================================}

_source = _use_tf ? security(syminfo.tickerid, _tf, source(_src)) : source(_src)

// Parabolic SAR
_psar = sar(_psar_start, _psar_increment, _psar_maximum)

// WaveTrend Oscillator
channel_index(x, type, len) =>
    mean = fast_moving_average(x, type, len)
    distance = fast_moving_average(abs(x - mean), type, len)
    (x - mean) / (0.015 * distance)
_ci = channel_index(_source, _fast_type, _channel_len)
_fast_wt = fast_moving_average(_ci, _fast_type, _fast_len)
_slow_wt = slow_moving_average(_fast_wt, _slow_type, _slow_len)

// Inverse Fisher of Trend Confirmation Index
tci_actual() =>
    (close - low) / ((high - low) / 100)
_tci = _use_tf ? security(syminfo.tickerid, _tf, tci_actual()) : tci_actual()
_tci_scale = (_tci - 50.0) * 0.1
_tci_average = moving_average(_tci_scale, _ma_type, _ma_len)
_ifish = (exp(2.0 * _tci_average) - 1.0) / (exp(2.0 * _tci_average) + 1.0)

//{=================================================================================== Strategy Signal =====================================================================================}

_long = _psar < close and _fast_wt > _slow_wt and rising(_ifish, 1)
_short = _psar > close and _fast_wt <_slow_wt and falling(_ifish, 1)

_xlong = falling(_ifish, 1)
_xshort = rising(_ifish, 1)

var _long_count = 0
var _short_count = 0

var _when_long = 0
var _when_short = 0
var _when_exit = 0

_in_long = _when_long > _when_short and _when_long > _when_exit
_in_short = _when_short > _when_long and _when_short > _when_exit
_in_exit = _when_exit > _when_long and _when_exit > _when_short

if _long
    if _in_short or _in_exit
        _long_count := 1
    else
        _long_count := nz(_long_count[1]) + 1
    _short_count := 0
    _in_short := false
    if _long_count <= _trades_count
        _when_long := time

if _short
    if _in_long or _in_exit
        _short_count := 1
    else
        _short_count := nz(_short_count[1]) + 1
    _long_count := 0
    _in_long := false
    if _short_count <= _trades_count
        _when_short := time

if (_xlong and _in_long) or (_xshort and _in_short)
    _when_exit := time

_enter_long = _when_long == time and bar_window() and (_market_position == "Long" or _market_position == "Both")
_enter_short = _when_short == time and bar_window() and (_market_position == "Short" or _market_position == "Both")
_exit_trade = _when_exit == time

strategy.entry("Long", strategy.long, when=_enter_long)
strategy.entry("Short", strategy.short, when=_enter_short)
strategy.close_all(when=_exit_trade)

//{==================================================================================== Strategy Draw ======================================================================================}

plot(_psar, style=plot.style_circles, linewidth=2, color=_psar < close ? #686ae5 : #feff54, transp=0, title="PSAR")
barcolor(_fast_wt > _slow_wt ? #63ff00 : #ff2700, title="WaveTrend")
plotshape(rising(_ifish, 1), style=shape.square, location=location.belowbar, color=#ebff00, title="IFish TCI Up")
plotshape(falling(_ifish, 1), style=shape.square, location=location.abovebar, color=#aaaaaa, title="IFish TCI Down")

plotshape(_enter_long, style=shape.triangleup, location=location.bottom, color=#1ca168)
plotshape(_enter_short, style=shape.triangleup, location=location.bottom, color=#ce2020)
plotshape(_exit_trade, style=shape.triangledown, location=location.bottom, color=#ffc100)
